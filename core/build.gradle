import me.champeau.gradle.japicmp.JapicmpTask
import io.micronaut.build.internal.japicmp.RemovedPackages

plugins {
    id "io.micronaut.build.internal.convention-core-library"
    id 'org.checkerframework' version '0.6.11'
}

apply plugin: 'org.checkerframework'

checkerFramework {
    checkers = [
            'org.checkerframework.checker.tainting.TaintingChecker',
            //'org.checkerframework.checker.tainting.TaintingChecker'
    ]
}

micronautBuild {
    core {
        documented = false
    }
}

dependencies {
    compileOnly libs.managed.jsr305
    compileOnly libs.managed.graal
    compileOnly libs.kotlin.stdlib
    compileOnly 'org.checkerframework:checker-qual:3.26.0'
    testCompileOnly 'org.checkerframework:checker-qual:3.26.0'
    checkerFramework 'org.checkerframework:checker:3.26.0'
}

spotless {
    java {
        targetExclude '**/io/micronaut/core/io/scan/AnnotationClassReader.java',
                '**/io/micronaut/core/io/scan/Attribute.java',
                '**/io/micronaut/core/io/scan/Context.java',
                '**/io/micronaut/core/util/clhm/**',
                '**/io/micronaut/core/util/AntPathMatcher.java'
    }
    format 'javaMisc', {
        targetExclude '**/io/micronaut/core/util/clhm/**'
    }
}

def versionInfo = tasks.register("micronautVersionInfo", WriteProperties) {
    outputFile = "${buildDir}/resources/version/micronaut-version.properties"
    property 'micronaut.version', projectVersion
}
tasks.named("processResources") {
    from(versionInfo)
}

tasks.withType(JapicmpTask).configureEach {
   richReport {
       addViolationTransformer(RemovedPackages, [prefixes: ['io.micronaut.caffeine'], exact: []])
   }
}

